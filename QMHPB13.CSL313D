     A                                      DSPSIZ(24 80 *DS3)
     A*========================================================================*
     A*                                                                        *
     A*       AEON CREDIT SERVICE SYSTEM                                       *
     A*                                                                        *
     A*       PROJECT ID  :  CC-9999-99-999                                    *
     A*       FILE ID     :  CSL313D                                           *
     A*       FUNCTION    :  SALES INQUIRY                                     *
     A*       AUTHOR      :  ACSS-SGALVEZ                                      *
     A*       CODING DATE :  2015/03/31                                        *
     A*                                                                        *
     A*========================================================================*
     A* MODIFICATION LOGS                                                      *
     A* ~~~~~~~~~~~~~~~~~                                                      *
     A* VER.   PROJECT          PIC            DATE        DESCRIPTION         *
     A*------------------------------------------------------------------------*
     A*                                                                        *
     A*========================================================================*
     A**************************************************************************
     A          R FMTHED
     A**************************************************************************
     A                                      OVERLAY
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  2  2USER
     A                                  1 25'CREDIT CARD SYSTEM  -'
     A                                  1 48'SALES INQUIRY'
     A                                      COLOR(BLU)
     A            FHPGMQ        10A  O  1  2
     A**************************************************************************
     A          R FMT010
     A**************************************************************************
     A                                      OVERLAY
     A                                      CF01(01 'EXIT')
     A                                      CF03(03 'MENU')
     A                                 10 22'Card Number   :'
     A            F1CNBR        16Y 0B 10 38EDTCDE(4)
     A  61                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PC)
     A                                 12 22'Expiry Date   :'
     A            F1EDTE         4Y 0B 12 38EDTWRD('  /  ')
     A*                                     EDTMSK('  &  ')
     A  62                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(PC)
     A                                 12 46'(MM/YY)'
     A**************************************************************************
     A          R FMT020
     A**************************************************************************
     A                                      OVERLAY
     A                                      CF01(01 'EXIT')
     A                                      CF02(02 'PREVIOUS')
     A                                      CF03(03 'MENU')
     A                                      CF12(12 'CANCEL')
     A                                  4  3'Card Number    :'
     A                                  4 52'I.D. Number   :'
     A                                  5  3'Member Name    :'
     A                                  7 19'Sales Date  :'
     A                                  8 19'Sales Time  :'
     A                                  9 19'Agent Name  :'
     A                                 10 20'Shop Name  :'
     A                                 11 17'Terminal No.  :'
     A                                 12 17'Product Code  :'
     A                                 13 19'Sales Type  :'
     A                                 14 17'Sales Amount  :'
     A                                 15 16'Reference No.  :'
     A                                 16 17'Posting Date  :'
     A                                 17 23'Status  :'
     A            F2CDNM        16Y 0O  4 20EDTWRD('    -    -    -    ')
     A            F2MBRN        30   O  5 20
     A            F2IDNO        11   O  4 68
     A            F2SDTE         8Y 0O  7 35EDTWRD('  /  /    ')
     A            F2STME         6Y 0O  8 35EDTWRD('  :  :  ')
     A            F2ACDE         2  0O  9 35
     A            F2ANME        10   O  9 39
     A            F2SHOP        30   O 10 35
     A            F2TERM        10   O 11 35
     A            F2PCDE         2  0O 12 35
     A            F2PDES        30   O 12 39
     A            F2STYP         2   O 13 35
     A            F2SAMT         9Y 0O 14 35EDTCDE(A)
     A            F2REFN        10   O 15 35
     A            F2PDTE         8Y 0O 16 35EDTWRD('  /  /    ')
     A            F2STSS        10   O 17 35
     A                                 18 23'Remarks :'
     A N75                                  DSPATR(ND)
     A            F2REM1        30   O 18 35
     A N75                                  DSPATR(ND)
     A            F2REM2        30   O 19 35
     A N75                                  DSPATR(ND)
     A**************************************************************************
     A          R SFL010                    SFL
     A**************************************************************************
     A                                      SFLNXTCHG
     A            S1OPTN         1   B 11  4
     A  66                                  DSPATR(ND PR)
     A  64                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            S1TDTE         8Y 0O 11  7EDTWRD('  /  /    ')
     A            S1STNO        10A  O 11 32
     A            S1STYP         2A  O 11 48
     A            S1AMNT         9Y 0O 11 54EDTCDE(1)
     A            S1STSS        10A  O 11 70
     A            S1REFN        10   O 11 19
     A**************************************************************************
     A          R CTL010                    SFLCTL(SFL010)
     A**************************************************************************
     A                                      CF01(01 'EXIT')
     A                                      CF02(02 'PREVIOUS')
     A                                      CF03(03 'MENU')
     A                                      CF12(12 'CANCEL')
     A                                      OVERLAY
     A N52                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  51                                  SFLCLR
     A N53                                  SFLEND(*MORE)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0010)
     A                                  9  3'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                 10  3'Opt Tran Date    Ref No.    Store -
     A                                      Ter No.  SalTyp       Amount     St-
     A                                      atus'
     A                                      COLOR(WHT)
     A                                  7  3'Card Number    :'
     A N63                                  DSPATR(ND)
     A                                  8  3'Member Name    :'
     A N63                                  DSPATR(ND)
     A                                  7 52'I.D. Number   :'
     A N63                                  DSPATR(ND)
     A            C1CNBR        16Y 0O  7 20EDTWRD('    -    -    -    ')
     A N63                                  DSPATR(ND)
     A            C1IDNO        11A  O  7 68
     A N63                                  DSPATR(ND)
     A            C1MNME        30A  O  8 20
     A N63                                  DSPATR(ND)
     A            C1RRN          4S 0H      TEXT('RRN')
     A                                      SFLRCDNBR(CURSOR)
     A                                  5  3'Type 1 on option, press Enter.'
     A                                      COLOR(BLU)
     A**************************************************************************
     A          R FMT030
     A**************************************************************************
     A                                      OVERLAY
     A                                      CF01(01 'EXIT')
     A                                      CF02(02 'PREVIOUS')
     A                                      CF03(03 'MENU')
     A                                      CF12(12 'CANCEL')
     A                                  7  3'Card Number    :'
     A                                  8  3'Member Name    :'
     A                                  7 52'I.D. Number   :'
     A            F3CNBR        16Y 0O  7 20EDTWRD('    -    -    -    ')
     A            F3IDNO        11A  O  7 68
     A            F3MNME        30A  O  8 20
     A                                  9  3'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                 10  3'Opt Tran Date    Ref No.    Store -
     A                                      Ter No.  SalTyp       Amount     St-
     A                                      atus'
     A                                      COLOR(WHT)
     A                                 21 74'Bottom'
     A                                      COLOR(WHT)
     A                                 14  3'< * * * * * * * * * * * * * * No R-
     A                                      ecords Found * * * * * * * * * * * -
     A                                      * * * >'
     A**************************************************************************
     A          R FMTCNF
     A**************************************************************************
     A                                      OVERLAY
     A                                      CF02(02 'PREVIOUS')
     A                                 22 64'Confirm(Y/N)'
     A            FCCCDE         1A  B 22 77
     A  82                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  82                                  DSPATR(PC)
     A                                      VALUES('Y' 'N' ' ')
     A**************************************************************************
     A          R FMTMSG
     A**************************************************************************
     A            FMERRM        78   O 23 02COLOR(RED)
     A                                      DSPATR(BL)
     A            FMFKEY        78   O 24  2COLOR(BLU)
